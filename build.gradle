plugins {
    id 'java'
    id 'maven-publish'
    id 'application'
    id 'idea'
}

def CLASSES_DIR = 'build/classes'
def SRC_DIR = 'src'
def SRC_GEN_DIR = 'src_gen'
def TESTS_DIR = 'tests'

sourceSets {
    main { java { srcDirs = [SRC_DIR, SRC_GEN_DIR] } }
    test { java { srcDirs = [TESTS_DIR] } }
}

compileJava {
    options.compilerArgs << "-s"
    options.compilerArgs << SRC_GEN_DIR

    doFirst {
        file(new File(projectDir, SRC_GEN_DIR)).mkdirs()
    }
}

repositories {
    mavenLocal()
    maven {
        url = uri('https://repo.maven.apache.org/maven2/')
    }
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
    implementation files('libs/graal.jar')
    implementation files('libs/graal-sdk.jar')
    implementation files('libs/truffle-api.jar')
    implementation files('libs/truffle-dsl-processor.jar')
}

run {
    jvmArgs "-Dtruffle.class.path.append=" + CLASSES_DIR
    standardInput = System.in
}

test {
    jvmArgs "-Dtruffle.class.path.append=" + CLASSES_DIR
    jvmArgs "--add-opens=org.graalvm.truffle/com.oracle.truffle.api=ALL-UNNAMED"
    jvmArgs "--add-opens=org.graalvm.truffle/com.oracle.truffle.api.nodes=ALL-UNNAMED"
    jvmArgs "--add-opens=org.graalvm.truffle/com.oracle.truffle.api.dsl=ALL-UNNAMED"
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

clean.doFirst {
    delete SRC_GEN_DIR
}

mainClassName = 'lox.GodClass'
